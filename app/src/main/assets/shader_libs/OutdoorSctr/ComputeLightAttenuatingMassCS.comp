#include "CloudsCommon.glsl"

#define MIN_ATTENUATION_THRESHOLD 0.01
float GetAttenuatingMassNormFactor()
{
	//exp( -fMaxMass * g_GlobalCloudAttribs.fAttenuationCoeff ) == MIN_ATTENUATION_THRESHOLD :
	return -log(MIN_ATTENUATION_THRESHOLD) / (g_GlobalCloudAttribs.fAttenuationCoeff * 0.1);
}

layout(r8, binding = 0) uniform image3D g_rwtex3DLightAttenuation;

layout (local_size_x = THREAD_GROUP_SIZE) in;

void main()
{
    uvec3 Gid = gl_WorkGroupID;
    uvec3 GTid = gl_LocalInvocationID;

    uint3 DstCellInd;
    uint uiLayer, uiRing;
    SCloudCellAttribs CellAttrs;
    float fLayerAltitude;
    float3 f3VoxelCenter;
    if( !VolumeProcessingCSHelperFunc(Gid, GTid, CellAttrs, uiLayer, uiRing, fLayerAltitude, f3VoxelCenter, DstCellInd) )
        return;

    float fTotalMass = 0;

    // Intersect the ray with the top of the cloud layer
    float3 f3EarthCentre = float3(0, -g_fEarthRadius, 0);
    float2 f2RayIsecs;
    float fCloudTopRadius = g_fEarthRadius + g_GlobalCloudAttribs.fCloudAltitude + g_GlobalCloudAttribs.fCloudThickness/2.f;
    float3 f3StartPos = f3VoxelCenter;
    float3 f3TraceDir = g_f4DirOnLight.xyz;
    GetRaySphereIntersection(f3StartPos, f3TraceDir, f3EarthCentre, fCloudTopRadius, f2RayIsecs);

    if( f2RayIsecs.y > 0.0 )
    {
        float fTraceLen = f2RayIsecs.y;
        fTraceLen = min( fTraceLen, g_GlobalCloudAttribs.fCloudThickness*4.f );
        const float NumSteps = 16.f;
        float fStepLen = fTraceLen/NumSteps;
        for(float fStep = 0.5f; fStep < NumSteps; ++fStep)
        {
            float3 f3CurrPos = f3StartPos + f3TraceDir * (fStepLen * fStep);
            float fCurrDensity = SampleCellAttribs3DTexture(g_tex3DCellDensity, f3CurrPos, uiRing, false);
            fTotalMass += fCurrDensity;
        }
        fTotalMass /= NumSteps;
        fTotalMass *= fTraceLen;
    }

//    g_rwtex3DLightAttenuation[ DstCellInd ] = fTotalMass / GetAttenuatingMassNormFactor();
    imageStore(g_rwtex3DLightAttenuation, int3(DstCellInd), float4(fTotalMass / GetAttenuatingMassNormFactor(), 0,0,0));
}